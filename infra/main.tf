terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 4.0.0"
    }
  }
}

provider "azurerm" {
  features {}
  subscription_id = var.subscription_id
}

## NOTE: Kubernetes and Helm provisioning (Ingress / cert-manager) are handled via
## the infra GitHub Actions workflow using Helm CLI rather than Terraform-managed helm_release resources.

data "azurerm_client_config" "current" {}

locals {
  project_name = "dotnetappazuredeploy"
  location     = "West Europe"
}

resource "azurerm_resource_group" "rg" {
  name     = "${local.project_name}-rg"
  location = local.location
}



resource "azurerm_application_insights" "insights" {
  name                = "${local.project_name}-ai"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  application_type    = "web"
  workspace_id        = azurerm_log_analytics_workspace.log_workspace.id
}

# SQL Server
resource "azurerm_mssql_server" "sql_server" {
  name                         = "${local.project_name}-sqlsrv"
  resource_group_name          = azurerm_resource_group.rg.name
  location                     = azurerm_resource_group.rg.location
  version                      = "12.0"
  administrator_login          = var.sql_admin_login
  administrator_login_password = var.sql_admin_password

  public_network_access_enabled = true
}

# Adding a firewall rule: Allow Azure Services
resource "azurerm_mssql_firewall_rule" "allow_azure_services" {
  name             = "AllowAllAzureServices"
  server_id        = azurerm_mssql_server.sql_server.id
  start_ip_address = "0.0.0.0"
  end_ip_address   = "0.0.0.0"
}

resource "azurerm_mssql_database" "sql_db" {
  name           = "${local.project_name}-db"
  server_id      = azurerm_mssql_server.sql_server.id
  sku_name       = "Basic"
  collation      = "SQL_Latin1_General_CP1_CI_AS"
  zone_redundant = false
}

# Key Vault
resource "azurerm_key_vault" "vault" {
  name                       = "${local.project_name}kv"
  location                   = azurerm_resource_group.rg.location
  resource_group_name        = azurerm_resource_group.rg.name
  tenant_id                  = data.azurerm_client_config.current.tenant_id
  sku_name                   = "standard"
  purge_protection_enabled   = false
  soft_delete_retention_days = 7

  access_policy {
    tenant_id = data.azurerm_client_config.current.tenant_id
    object_id = data.azurerm_client_config.current.object_id

    secret_permissions = [
      "Get", "Set", "List", "Delete"
    ]
  }
}

# Secret with connection string
resource "azurerm_key_vault_secret" "sql_connection_string" {
  name         = "ConnectionStrings--DefaultConnection"
  value        = var.connection_string
  key_vault_id = azurerm_key_vault.vault.id
}

# Log Analytics workspace (used by AKS/monitoring)
resource "azurerm_log_analytics_workspace" "log_workspace" {
  name                = "${local.project_name}-log"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  sku                 = "PerGB2018"
  retention_in_days   = 30
}

# Container Registry
resource "azurerm_container_registry" "acr" {
  name                = "${local.project_name}acr"
  resource_group_name = azurerm_resource_group.rg.name
  location            = azurerm_resource_group.rg.location
  sku                 = "Basic"
  admin_enabled       = true
}

resource "azurerm_monitor_diagnostic_setting" "acr_diag" {
  name                       = "acr-diagnostics"
  target_resource_id         = azurerm_container_registry.acr.id
  log_analytics_workspace_id = azurerm_log_analytics_workspace.log_workspace.id

  enabled_log {
    category = "ContainerRegistryRepositoryEvents"
  }

  enabled_log {
    category = "ContainerRegistryLoginEvents"
  }

  enabled_metric {
    category = "AllMetrics"
  }
}

# Kubernetes (AKS) cluster
resource "azurerm_kubernetes_cluster" "aks" {
  name                = "${local.project_name}-aks"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  dns_prefix          = "${local.project_name}-dns"

  default_node_pool {
    name       = "default"
    node_count = 1
    vm_size    = "Standard_B2s" # or "Standard_DS2_v2"
  }

  identity {
    type = "SystemAssigned"
  }

  network_profile {
    network_plugin = "azure"
  }

  depends_on = [azurerm_container_registry.acr]
}

# Grant AKS kubelet identity permissions to pull from ACR
resource "azurerm_role_assignment" "acr_pull" {
  principal_id         = azurerm_kubernetes_cluster.aks.kubelet_identity[0].object_id
  scope                = azurerm_container_registry.acr.id
  role_definition_name = "AcrPull"
}

###### Helm: ingress-nginx and cert-manager

# NOTE: Ingress controller (ingress-nginx) and cert-manager are installed via Helm CLI in
# the `infra` GitHub Actions workflow after Terraform `apply` completes. The Helm provider
# blocks were removed from Terraform to simplify provisioning and avoid kubeconfig/provider
# bootstrap complexity in Terraform runs.